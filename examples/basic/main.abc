constexpr sprites BALL = sprites{
    8x8
    --XXXX--
    -X....X-
    X......X
    X......X
    X......X
    X......X
    -X....X-
    --XXXX--
};

int x = 60;
int y = 28;

int coins = 0;

constexpr tones SONG = tones{
    ItsGonna:d=4,o=5,b=90:
    16g,16f,16d#,16d,8c,8g,2f,16f,16d#,16d,16c,8a#4,8f,2d#,
    16d#,16d,16c,16a#4,8c.,8d#.,8g,2f,8g.,8f.,8g,f,16g,16f,
    16d#,16d,8c.,8g.,2f,16d,16c,8a#.4,8f.,d#.,d,c.,8d#,16d,
    16c,8c,c,2p
};

void loop()
{
    if($just_pressed(A_BUTTON))
    {
        $tones_play_auto(tones{ B5 100 E6 200 });
        ++coins;
    }
    
    if($just_pressed(B_BUTTON))
        $tones_play_primary(SONG);
    
    $draw_text(0, 5,
        "Hello World!\n"
        "Press A to collect a coin.\n"
        "Press B if it's gonna be you.");
    $draw_textf(0, 30, "Coins collected: %d", coins);
    $draw_textf(0, 55, "Running for %.2f seconds", float($millis()) / 1000);
    
    if($pressed(UP_BUTTON   )) --y;
    if($pressed(DOWN_BUTTON )) ++y;
    if($pressed(LEFT_BUTTON )) --x;
    if($pressed(RIGHT_BUTTON)) ++x;
    $draw_sprite(x, y, BALL, 0);
    
    $display();
}

void main()
{
    $set_frame_rate(50);
    $set_text_font(FONT_BR5D);
    while(true)
        loop();
}
