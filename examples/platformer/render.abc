constexpr sprites TILES = sprites{ 16x16 "assets/tiles.png" };
constexpr sprites OBJECTS = sprites{ 16x16 "assets/objects.png" };
constexpr sprites PLAYER = sprites{ 16x16 "assets/player.png" };
constexpr sprites IMG_KEY = sprites{ "assets/key.png" };
constexpr sprites IMG_HEART = sprites{ 10x8 "assets/heart.png" };
constexpr sprites TITLE = sprites{ "assets/title.png" };

constexpr font MAIN_FONT = FONT_VOLTER;
constexpr font SCORE_FONT = font{ 9 "assets/score.ttf" };

void render()
{
    if(state == STATE_LEVEL)
        render_level();
    else if(state == STATE_LEVEL_COMPLETE)
        render_level_complete();
    else if(state == STATE_PAUSE)
        render_pause();
    else if(state == STATE_INTRO || state == STATE_SCORE_TALLY)
        render_intro();
    else if(state == STATE_MENU)
        render_menu();
    else if(state == STATE_VICTORY)
        render_victory();
    else if(state == STATE_LEVEL_SELECT)
        render_level_select();
    else if(state == STATE_GAME_OVER)
        render_game_over();
}

void render_pause()
{
    render_level();
}

u8[40] prog GAME_OVER_TILES = {
    0, 0, 0, 0, 0, 0, 133, 0,
    0, 0, 0, 0, 0, 0, 133, 0,
    0, 0, 0, 0, 0, 93, 133, 0,
    0, 0, 0, 0, 0, 0, 149, 150,
    2, 2, 2, 2, 2, 2, 2, 2
};

void render_game_over()
{
    for(u8 i = 0; i < 5; ++i)
        for(u8 j = 0; j < 8; ++j)
        {
            u8 t = VICTORY_TILES[i * 8 + j] - 1;
            if(t < 240)
                $draw_sprite(j * 16, i * 16 - 8, TILES, t);
        }
    
    $draw_text_P(1, 0, MAIN_FONT, "Game Over...");

    {
        u8 i = (frame_count >> 4) & 3;
        $draw_sprite(76, 8, OBJECTS, 8 + i * 16);
        $draw_sprite(27, 15, OBJECTS, 12 + i * 16);
        i = (i + 1) & 3;
        $draw_sprite(13, 39, OBJECTS, 10 + i * 16);
        i = (i + 1) & 3;
        $draw_sprite(38, 39, OBJECTS, 9 + i * 16);
        i = (i + 1) & 3;
        $draw_sprite(74, 39, OBJECTS, 9 + i * 16);
        $draw_sprite(58, 21, OBJECTS, 11 + i * 16);
        $draw_sprite(56, 39, OBJECTS, 64);
    }
}

u8[40] prog VICTORY_TILES = {
    0, 0, 0, 0, 0, 0, 133, 0,
    0, 0, 0, 0, 0, 0, 133, 0,
    0, 0, 0, 0, 0, 93, 133, 0,
    0, 0, 0, 0, 0, 148, 149, 150,
    2, 2, 2, 2, 2, 2, 2, 2
};

u8[8] prog VICTORY_FRAMES = {
    0, 0, 6, 5, 6, 0, 0, 0
};
i8[8] prog VICTORY_OFFY = {
    8, 8, 8, 8, 8, 8, 8, 8
};

void render_victory()
{
    for(u8 i = 0; i < 5; ++i)
        for(u8 j = 0; j < 8; ++j)
        {
            u8 t = VICTORY_TILES[i * 8 + j] - 1;
            if(t < 240)
                $draw_sprite(j * 16, i * 16 - 8, TILES, t);
        }
    
    $draw_text_P(1, 0, MAIN_FONT, "Game Complete!");

    constexpr u8 x = 13;
    constexpr u8 y = 12;
    $draw_textf(x + 6, y + 1, SCORE_FONT, "%06u", total_score);
    if(was_high_score)
        $draw_text_P(x, y + 12, FONT_BR6, "New High Score!");
    {
        char[10] buf;
        $format(buf, "%u.%02u", total_time / 50, (total_time % 50) * 2);
        u8 w = $text_width(SCORE_FONT, buf);
        $draw_text(x + 27 - w / 2, y + 23, SCORE_FONT, buf);
    }
    if(was_best_time)
        $draw_text_P(x + 1, y + 35, FONT_BR6, "New Best Time!");

    {
        u8 i = (frame_count >> 3) & 7;
        $draw_sprite(76, VICTORY_OFFY[i], PLAYER, VICTORY_FRAMES[i]);
    }
}

void render_level_select()
{
    $draw_text_P(10, 0, FONT_BR6, "Name");
    $draw_text_P(60, 0, FONT_BR6, "High Score");
    $draw_text_P(97, 0, FONT_BR6, "Best Time");

    for(u8 i = 0; i < LEVEL_SELECT_ROWS; ++i)
    {
        u8 j = i + levelseloff;
        if(j >= NUM_MAPS) break;
        u8 y = i * 7 + 9;
        if(i == levelseli)
            $draw_text_P(0, y, FONT_BR6, "->");
        else
            $draw_textf(0, y, FONT_BR6, "%02u", j + 1);
        $draw_text_P(10, y, FONT_BR6, maps[j].name);
        $draw_filled_rect(57, y, 71, 6, BLACK);

        if(map_high_scores[j] != 0)
            $draw_textf(67, y, FONT_BR6, "%05u", map_high_scores[j]);
        else
            $draw_text_P(75, y, FONT_BR6, "-");

        if(map_best_times[j] != 0)
        {
            u16 t = map_best_times[j];
            char[10] buf;
            $format(buf, "%u.%02u", t / 50, (t % 50) * 2);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(124 - w, y, FONT_BR6, buf);
        }
        else
            $draw_text_P(110, y, FONT_BR6, "-");
    }

    {
        constexpr u8 y = LEVEL_SELECT_ROWS * 7 + 9;
        constexpr u8 y2 = y + 16;
        $draw_filled_rect(0, 7, 128, 1, WHITE);
        $draw_filled_rect(0, y, 128, 1, WHITE);
        $draw_filled_rect(0, y2, 128, 1, WHITE);
        $draw_filled_rect(8, 0, 1, y2, WHITE);
        $draw_filled_rect(58, 0, 1, y2, WHITE);
        $draw_filled_rect(95, 0, 1, y2, WHITE);
        $draw_text_P(10, y + 2, FONT_BR6, "Entire Game");
        $draw_text_P(10, y + 9, FONT_BR6, "Sum of Best");

        bool has_sum_score = true;
        bool has_sum_time = true;
        u24 sum_score = 0;
        u24 sum_time = 0;
        for(u8 i = 0; i < NUM_MAPS; ++i)
        {
            if(map_high_scores[i] != 0)
                sum_score += map_high_scores[i];
            else
                has_sum_score = false;
            if(map_best_times[i] != 0)
                sum_time += map_best_times[i];
            else
                has_sum_time = false;
        }

        if(high_score != 0)
            $draw_textf(65, y + 2, FONT_BR6, "%06u", high_score);
        else
            $draw_text_P(75, y + 2, FONT_BR6, "-");
        
        if(has_sum_score)
            $draw_textf(65, y + 9, FONT_BR6, "%06u", sum_score);
        else
            $draw_text_P(75, y + 9, FONT_BR6, "-");
        
        if(best_time != 0)
        {
            char[10] buf;
            $format(buf, "%u.%02u", best_time / 50, (best_time % 50) * 2);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(124 - w, y + 2, FONT_BR6, buf);
        }
        else
            $draw_text_P(110, y + 2, FONT_BR6, "-");
        
        if(has_sum_time)
        {
            char[10] buf;
            $format(buf, "%u.%02u", sum_time / 50, (sum_time % 50) * 2);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(124 - w, y + 9, FONT_BR6, buf);
        }
        else
            $draw_text_P(110, y + 9, FONT_BR6, "-");
    }
}

void render_menu()
{
    $draw_sprite_selfmask(13, 2, TITLE, 0);
    $draw_sprite(menu_spritex - 16, -2, OBJECTS, menu_sprite + (frame_count & 0x30));
    $draw_filled_rect(0, 14, 128, 2, WHITE);
    constexpr u8 x = 33;
    constexpr u8 y = 21;
    constexpr u8 h = 11;
    if(current_map != 0)
        $draw_text_P(x, y, MAIN_FONT, "Resume Game");
    else 
        $draw_text_P(x, y, MAIN_FONT, "New Game");
    $draw_text_P(x, y + h * 1, MAIN_FONT, "Level Select");
    if(sound_on)
        $draw_text_P(x, y + h * 2, MAIN_FONT, "Sound: On");
    else
        $draw_text_P(x, y + h * 2, MAIN_FONT, "Sound: Off");
    $draw_text_P(x, y + h * 3, MAIN_FONT, "Credits");
    $draw_sprite(x - 19, y - 5 + menui * h, PLAYER, 7 + ((frame_count >> 3) & 3));
}

void render_intro()
{
    //$draw_text_P(55, 10, FONT_BR6, "Score");
    $draw_textf(43, 18, SCORE_FONT, "%06u", total_score);
    //$draw_rect(41, 16, 45, 13, WHITE);
    if(level_frames_remaining != 0)
    {
        $draw_text_P(25, -1, MAIN_FONT, "Level Complete!");
        $draw_textf(41, 30, FONT_BR6, "%05u", score);
        char[10] buf;
        $format(buf, "%u.%02u",
            level_frames_remaining / 50,
            (level_frames_remaining % 50) * 2);
        u8 w = $text_width(FONT_BR6, buf);
        $draw_text(87 - w, 30, FONT_BR6, buf);
    }
    else
    {
        if(current_map >= NUM_MAPS)
            $draw_text_P(24, -1, MAIN_FONT, "Congratulations!");
        else
            $draw_text_P(37, -1, MAIN_FONT, "Get Ready!");

        if(total_score != 0)
        {
            constexpr u8 y = 16;
            $draw_text_P(94, y + 0, FONT_BR6, "Total Time");
            $draw_filled_rect(94, y + 6, 34, 1, WHITE);
            char[10] buf;
            $format(buf, "%u.%02u",
                total_time / 50,
                (total_time % 50) * 2);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(111 - w / 2, y + 8, FONT_BR6, buf);
        }
    }
    if(state == STATE_INTRO && level_frames_remaining != 0)
    {
        constexpr u8 y = 40;
        if(was_best_time)
        {
            $draw_text_P(93, y - 8, FONT_BR6, "New Level");
            $draw_text_P(93, y - 1, FONT_BR6, "Best Time!");
            $draw_filled_rect(91, y + 6, 37, 1, WHITE);
            char[10] buf;
            u16 time_taken = TIME_LIMIT_FRAMES - level_frames_remaining;
            $format(buf, "%u.%02u",
                time_taken / 50,
                (time_taken % 50) * 2);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(110 - w / 2, y + 8, FONT_BR6, buf);
        }
        if(was_high_score)
        {
            $draw_text_P(2, y - 8, FONT_BR6, "New Level");
            $draw_text_P(1, y - 1, FONT_BR6, "High Score!");
            $draw_filled_rect(0, y + 6, 37, 1, WHITE);
            char[10] buf;
            u16 map_score = score + level_frames_remaining;
            $format(buf, "%05u", map_score);
            u8 w = $text_width(FONT_BR6, buf);
            $draw_text(17 - w / 2, y + 8, FONT_BR6, buf);
        }
    }
    $draw_sprite(56, 37, PLAYER, 7 + ((frame_count >> 3) & 0x3));
    $draw_textf(72, 44, FONT_BR6, "x%u", lives);
    if((frame_count & 0x3f) < 0x30)
        $draw_text_P(44, 56, MAIN_FONT, "Press A");
}

void render_level_complete()
{ 
    render_level();
}

void render_level()
{
    // setup
    int icamx = int($round(camx));
    int icamy = int($round(camy));

    // render map tiles
    {
        i8 cx = icamx / 16;
        i8 cy = icamy / 16;
        i8 xoff = -(icamx & 15);
        i8 yoff = -(icamy & 15);
        u8[] prog& m = maps[current_map].map;
        u8 w = map_width;

        // background pattern
        {
            sprites background = maps[current_map].background;
            constexpr u8 SHIFT = 2;
            i16 tx = -((icamx >> SHIFT) & 255);
            u16 h = map_height * 16 - (icamy + 64);
            i16 ty = ((h >> SHIFT) & 63);
            $draw_sprite(tx      , ty - 64, background, 0);
            $draw_sprite(tx + 128, ty - 64, background, 1);
            if(h >= 64 * (1 << SHIFT))
            {
                if(tx >= -128)
                    $draw_sprite(tx      , ty, background, 0);
                else
                    $draw_sprite(tx + 256, ty, background, 0);
                $draw_sprite(tx + 128, ty, background, 1);
            }
            else
            {
                if(tx >= -128)
                    $draw_sprite(tx      , ty, background, 2);
                else
                    $draw_sprite(tx + 256, ty, background, 2);
                $draw_sprite(tx + 128, ty, background, 3);
            }
        }

        for(u8 i = 0; i < 5; ++i)
        {
            u16 index = (i + cy) * w + cx;
            for(u8 j = 0; j < 9; ++j)
            {
                u8 t = m[index + j] - 1;
                if(t >= 240)
                    continue;
                if(t >= 176)
                    t += ((frame_count << 2) & 0x30);
                $draw_sprite(j * 16 + xoff, i * 16 + yoff, TILES, t);
            }
        }
    }

    // render objects
    {
        for(u8 i = 0; i < MAX_OBJECTS; ++i)
        {
            map_object_t obj = objects[i];
            u8 t = obj.type;
            if(t == OBJ_NONE)
                continue;
            
            u8 index;
            if(t >= OBJ_MOB && obj.death_frames != 0)
            {
                if(obj.death_frames & 0x4)
                    continue;
                else
                    index = t + 64;
            }
            else
            {
                index = t;
                if(death_frames == 0)
                    index += ((frame_count + i * 173) & 0x30);
            }

            $draw_sprite(
                obj.x - icamx, obj.y - icamy, OBJECTS,
                index);
        }
    }

    // render player
    if(invincibility_frames == 0 || (frame_count & 0x04) == 0)
    {
        u8 index = 0;
        if(player_facing_right)
            index += 7;
        if(player_on_ground)
        {
            if(player_moving)
                index += ((frame_count >> 3) & 3);
        }
        else
        {
            index += 4;
        }
        if(death_frames != 0)
            index = 5;

        float x = px - camx - PXOFF;
        i16 y = $round(py) - icamy - i16(PYOFF);
        $draw_sprite($round(x), y, PLAYER, index);
    }
   
    // render HUD elements
    for(u8 i = 0; i < MAX_HEARTS; ++i)
        $draw_sprite(119 - i * 9, 0, IMG_HEART, i < hearts);
    for(u8 i = 0; i < keys; ++i)
        $draw_sprite(119 - i * 9, 8, IMG_KEY, 0);
    $draw_filled_rect(0, 0, 20, 6, BLACK);
    $draw_textf(0, 0, FONT_BR6, "%05u", score);
    $draw_filled_rect(57, 0, 13, 6, BLACK);
    $draw_textf(58, 0, FONT_BR6, "%03u", u16(level_frames_remaining + 49) / 50);

    // render level start animation
    if(level_start_frames != 0)
    {
        //char[20] buf;
        //$format(buf, "Level %d", current_map + 1);
        //u8 w = $text_width(MAIN_FONT, buf);
        char[] prog& name = maps[current_map].name;
        u8 w = $text_width_P(MAIN_FONT, name);
        constexpr u8 y = 24;
        constexpr u8 h = 13;
        i16 x = 64 - w / 2;
        i16 xo = 0;
        constexpr u8 TRANSITION_FRAMES = 12;
        constexpr u8 END_FRAME = LEVEL_START_FRAMES - TRANSITION_FRAMES;
        if(level_start_frames >= END_FRAME)
            xo += (level_start_frames - END_FRAME) * 10;
        if(level_start_frames <= TRANSITION_FRAMES)
            xo -= (TRANSITION_FRAMES - level_start_frames) * 10;
        $draw_filled_rect(xo, y - 1, 128, h, BLACK);
        //$draw_text(x, y, MAIN_FONT, buf);
        $draw_text_P(x + xo, y, MAIN_FONT, name);

        --level_start_frames;
    }
}
