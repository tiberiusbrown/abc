constexpr font f = FONT_BR5D;

constexpr sprites BALL = sprites{
    8x8
    --XXXX--
    -X....X-
    X......X
    X......X
    X......X
    X......X
    -X....X-
    --XXXX--
};

int x = 60;
int y = 28;

int coins = 0;

constexpr tones SONG = tones{
Tetris:d=4,o=6,b=80:8f7,16c7,16c_7,16d_7,32f7,32d_7,16c_7,16c7,8a_,16a_,16c_7,8f7,16d_7,16c_7,8c.7,16c_7,8d_7,8f7,8c_7,8a_,a_,8d_7,16f_7,8a_7,16g_7,16f_7,8f.7,16c_7,8f7,16d_7,16c_7,8c7,16c7,16c_7,8d_7,8f7,8c_7,8a_,8a_

};

void loop()
{
    if($just_pressed(A_BUTTON))
    {
        $tones_play(tones{ B5 100 E6 200 });
        ++coins;
    }
    
    if($just_pressed(B_BUTTON))
        $tones_play(SONG);
    
    $draw_text_P(0, 5, "Hello World!\nPress A to collect a coin.");
    $draw_textf(0, 25, "Coins collected: %d", coins);
    $draw_textf(0, 55, "Running for %.2f seconds", float($millis()) / 1000);
        
    if($pressed(UP_BUTTON   )) --y;
    if($pressed(DOWN_BUTTON )) ++y;
    if($pressed(LEFT_BUTTON )) --x;
    if($pressed(RIGHT_BUTTON)) ++x;
    $draw_sprite(x, y, BALL, 0);
    
    $display();
}

void main()
{
    $set_frame_rate(50);
    $set_text_font(f);
    while(true)
        loop();
}
